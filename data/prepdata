#!/bin/tcsh
#+
#  Script for preparing data for plots. Currently set up to run on octopus,
#  but you can fiddle dataroot which is the path to a tree resembling
# /jcmtdata/raw/scuba2 at JAC
#

set dataroot = /scuba2
set msg = quiet

# extra cleaning parameters
set clean_extra = "spikethresh=5,spikebox=50"


# the following outputs are used by intro.pro and pca2band.pro -----------------

# Uranus data from cookbook
#set obsdate = 20091214
#set obsnum = 00015
#set subfile = "000[23]"

# Wayne's debris disk from cookbook
set obsdate = 20100313
set obsnum = 00029
set subfile = "001[345]"

# Some Lockman data
#set obsdate = 20100311
#set obsnum = 00065
#set subfile = "000[345]"

echo "*** Processing $obsdate $obsnum ***"

foreach lambda (s4a s8d)
    echo "--- $lambda ---"

    echo "    Concatenating..."

    set basename = $lambda$obsdate\_$obsnum

    $SMURF_DIR/sc2concat \
        $dataroot/$lambda/$obsdate/$obsnum/$basename\_"$subfile" \
        $lambda$obsdate\_$obsnum\_con msg_filter=$msg

    echo "    Cleaning..."

    $SMURF_DIR/sc2clean $basename\_con \
        $basename\_con_clean \
        config='"^$STARLINK_DIR/share/smurf/dimmconfig.lis,'$clean_extra'"' msg_filter=$msg

    echo "    Removing common-mode..."

    $SMURF_DIR/sc2clean $basename\_con \
        $basename\_con_nocom \
        config='"^$STARLINK_DIR/share/smurf/dimmconfig.lis,'$clean_extra',compreprocess=1,com.boxcar=0,com.gain_box=600000"' \
        msg_filter=$msg

    echo "    Copy out QUALITY at single time slice as a mask..."

    $KAPPA_DIR/ndfcopy "$basename\_con_nocom(,,1000)" \
        mask_$lambda comp=qual trim trimwcs msg_filter=$msg

    echo "    Convert to FITS..."

    $CONVERT_DIR/ndf2fits $basename\_con_clean \
        \!$basename\_con_clean.fits msg_filter=$msg

    $CONVERT_DIR/ndf2fits $lambda$obsdate\_$obsnum\_con_nocom \
        \!$basename\_con_nocom.fits msg_filter=$msg

    $CONVERT_DIR/ndf2fits mask_$lambda \!mask_$lambda.fits msg_filter=$msg

end

echo "    Getting JCMTState..."

$SMURF_DIR/jcmtstate2cat s4a$obsdate\_$obsnum\_con > \
    state_$obsdate\_$obsnum.tst


